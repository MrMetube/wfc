#+vet !unused-procedures
package main

import "base:builtin"
import "base:intrinsics"

Array :: struct ($T: typeid) {
    data:  []T,
    count: i64,
}
String_Builder :: Array(u8)
FixedArray :: struct ($N: i64, $T: typeid) {
    data:  [N]T,
    count: i64,
}

append :: proc { 
    append_fixed_array, append_array, append_array_, append_array_many, append_fixed_array_many, append_string, 
    builtin.append_elem, builtin.append_elems, builtin.append_soa_elems, builtin.append_soa_elem, 
}
@(require_results) append_array_ :: proc(a: ^Array($T)) -> (result: ^T) {
    result = &a.data[a.count]
    a.count += 1
    return result
}
append_array :: proc(a: ^Array($T), value: T) -> (result: ^T) {
    a.data[a.count] = value
    result = append_array_(a)
    return result
}
append_fixed_array :: proc(a: ^FixedArray($N, $T), value: T) -> (result: ^T) {
    a.data[a.count] = value
    result = &a.data[a.count]
    a.count += 1
    return result
}
append_array_many :: proc(a: ^Array($T), values: []T) -> (result: []T) {
    start := a.count
    for &value in values {
        a.data[a.count] = value
        a.count += 1
    }
    
    result = a.data[start:a.count]
    return result
}
append_fixed_array_many :: proc(a: ^FixedArray($N, $T), values: []T) -> (result: []T) {
    start := a.count
    for &value in values {
        a.data[a.count] = value
        a.count += 1
    }
    
    result = a.data[start:a.count]
    return result
}

append_string :: proc(a: ^String_Builder, value: string) -> (result: string) {
    return cast(string) append_array_many(a, transmute([]u8) value)
}

make_string_builds :: proc(arena: ^Arena, #any_int len: i32, params := DefaultPushParams) -> (result: String_Builder) {
    result.data = push_slice(arena, u8, len, params)
    return result
}
make_array :: proc(arena: ^Arena, $T: typeid, #any_int len: i32, params := DefaultPushParams) -> (result: Array(T)) {
    result.data = push_slice(arena, T, len, params)
    return result
}

slice :: proc{ slice_fixed_array, slice_array, slice_array_pointer }
slice_fixed_array :: proc(array: ^FixedArray($N, $T)) -> []T {
    return array.data[:array.count]
}
slice_array :: proc(array: Array($T)) -> []T {
    return array.data[:array.count]
}
slice_array_pointer :: proc(array: ^Array($T)) -> []T {
    return array.data[:array.count]
}

to_string :: proc(array: String_Builder) -> string {
    return cast(string) array.data[:array.count]
}

rest :: proc{ rest_fixed_array, rest_array }
rest_fixed_array :: proc(array: ^FixedArray($N, $T)) -> []T {
    return array.data[array.count:]
}
rest_array :: proc(array: Array($T)) -> []T {
    return array.data[array.count:]
}

clear :: proc { array_clear, builtin.clear_dynamic_array, builtin.clear_map, }
array_clear :: proc(a: ^Array($T)) {
    a.count = 0
}

ordered_remove :: proc { builtin.ordered_remove, ordered_remove_array }
ordered_remove_array :: proc(a: ^Array($T), #any_int index: i64) {
    data := slice(a^)
    copy(data[index:], data[index+1:])
    a.count -= 1
}
unordered_remove :: proc { builtin.unordered_remove, unordered_remove_array }
unordered_remove_array :: proc(a: ^Array($T), #any_int index: i64) {
    swap(&a.data[index], &a.data[a.count-1])
    a.count -= 1
}

////////////////////////////////////////////////
// [First] <- [..] ... <- [..] <- [Last] 
Deque :: struct($L: typeid) {
    first, last: ^L,
}

deque_prepend :: proc(deque: ^Deque($L), element: ^L) {
    if deque.first == nil {
        assert(deque.last == nil)
        deque.last  = element
        deque.first = element
    }  else {
        element.next = deque.last
        deque.last   = element
    }
}

deque_append :: proc(deque: ^Deque($L), element: ^L) {
    if deque.first == nil {
        assert(deque.last == nil)
        deque.last  = element
        deque.first = element
    }  else {
        deque.first.next = element
        deque.first      = element
    }
}

deque_remove_from_end :: proc(deque: ^Deque($L)) -> (result: ^L) {
    result = deque.last
    
    if result != nil {
        deque.last = result.next

        if result == deque.first {
            assert(result.next == nil)
            deque.first = nil
        }
    }
    
    return result
}

////////////////////////////////////////////////
// Double Linked List
// [Sentinel] -> <- [..] ->
//  -> <- [..] -> ...    <-

list_init_sentinel :: proc(sentinel: ^$T) {
    sentinel.next = sentinel
    sentinel.prev = sentinel
}

list_prepend :: proc(list: ^$T, element: ^T) {
    element.prev = list.prev
    element.next = list
    
    element.next.prev = element
    element.prev.next = element
}

list_append :: proc(list: ^$T, element: ^T) {
    element.next = list.next
    element.prev = list
    
    element.next.prev = element
    element.prev.next = element
}

list_remove :: proc(element: ^$T) {
    element.prev.next = element.next
    element.next.prev = element.prev
    
    element.next = nil
    element.prev = nil
}

///////////////////////////////////////////////
// Single Linked List
// [Head] -> [..] ... -> [..] -> [Tail]

list_push :: proc { list_push_next, list_push_custom_member }
list_push_next          :: proc (head: ^^$T, element: ^T)             { list_push(head, element, offset_of(T, next)) }
list_push_custom_member :: proc (head: ^^$T, element: ^T, $next: umm) {
    element_next := get(element, next) 
    #assert(type_of(element_next^) == ^T)

    element_next ^= head^
    head         ^= element
}

list_pop_head :: proc { list_pop_head_custom_member, list_pop_head_next }
list_pop_head_next          :: proc (head: ^^$T)             -> (result: ^T, ok: b32) #optional_ok { return list_pop_head(head, offset_of(head^.next)) }
list_pop_head_custom_member :: proc (head: ^^$T, $next: umm) -> (result: ^T, ok: b32) #optional_ok {
    if head^ != nil {
        result = head^
        head ^= get(result, next)^
        
        ok = true
    }
    return result, ok
}

///////////////////////////////////////////////

@(private="file") 
get :: proc (type: ^$T, $offset: umm ) -> (result: ^^T) {
    raw_link := cast([^]u8) type
    slot := cast(^^T) &raw_link[offset]
    return slot
}
